=title Raku Interactive Shell - values in $ variables
=timestamp 2020-08-26T07:32:02
=indexes list, keys, values, Int, Pair, Hash, List, Array
=status show
=books raku
=author szabgab
=archive 1
=comments_disqus_enable 0

=abstract start

When learning Raku, one of the best things you can do is to experiment with the language in the interactive shell.
(Also known as <a href="https://en.wikipedia.org/wiki/Read%E2%80%93eval%E2%80%93print_loop">REPL = Read Eval Print Loop</a>)

Once you have <a href="https://rakudo.org/">Rakudo</a> installed, open a Terminal (or Command Prompt in MS Windows)
and type in <b>raku</b>. If you are using the Docker version of Rakudo Star then run it in interactive mode as

<code>
docker run --rm -it rakudo-star
</code>

After a second or so, you will see:

=abstract end

<code>
To exit type 'exit' or '^D'
>
</code>

The &gt; sign is the prompt. You can now type in any Raku expression.
Let's start with a few simple ones:

<code>
> say "hello world"
hello world
> say 42
42
> 19 + 23
42
</code>

<hl>say</hl> is the keyword to <b>print</b> something to the screen and add a trailing newline.

Strings are in double-quotes. Numbers don't have quotes.

We can use Raku as a calculator. In the interactive shell we don't even need to call <b>say</b>
as it will automatically print the result of the last statement. (The P is in the name REPL for a reason.)


<h2>Variables</h2>

Most variables in Raku have one or two special characters at the beginning. The first such character is called
Sigil, the second, optional character is called Twigil.

The <hl>$</hl> sigil means the variable can contain any item.
Let's try to assign a number to a variable that starts with a <hl>$</hl>:

<code>
> $x = 42
===SORRY!=== Error while compiling:
Variable '$x' is not declared
------> <BOL>‚èè$x = 42
</code>

Even the interactive shell requires us to declare our variables using the <hl>my</hl> keyword:

<code>
> my $x = 42
42
> $x
42
> $x - 7
35
</code>

After the declaration and the assignment, we can use the variable in arithmetic operations.

Using the <hl>^name</hl> method we can also ask the variable what type is it. In this case it is of type <hl>Int</hl>:

<code>
> $x.^name
Int
</code>


We can replace the content of the variable with a string, and then, when we ask what is
the <b>^name</b> of its type, we get back that it is a <b>Str</b>.

<code>
> $x = "abc"
abc

> $x.^name
Str
</code>

Some people love this kind of flexibility, for others this looks crazy dangerous. Don't worry,
In Raku, you can be much stricter with your variable types during declaration. If that's your thing:

<code>
> my Int $w = 42
42
> $w.^name
Int

> $w = "hello"
Type check failed in assignment to $w; expected Int but got Str ("hello")
  in block <unit> at <unknown file> line 1

>
</code>

In this case, we've declared the $w variable to be <hl>Int</hl> so we won't be able to
assign any other type to it.


Back to our type declaration free variable, you can put all kind of other types in that variable:

<code>
> $x = 3.14
3.14
> $x.^name
Rat

> $x = ["foo", "bar", "qux"]
[foo bar qux]
> $x.^name
Array

> $x = ("foo", "bar", "qux")
(foo bar qux)
> $x.^name
List

> $x = {name => "Foo", answer => 42}
{answer => 42, name => Foo}
> $x.^name
Hash

> $x = planet => "Earth"
platent => Earth
> $x.^name
Pair
</code>


We'll look at these types later on.

<h2>Pair</h2>

<code>
> $x = planet => "Earth"
planet => Earth
> $x.^name
Pair
> $x.gist
planet => Earth
> $x.key
planet
> $x.value
Earth
</code>

<h2>Hash</h2>

A Hash is a key-value store. In other languages it might be called <hl>Associative Array</hl> or <hl>Dictionary</hl>.

We can create a hash using curly-braces, and assign it to a variable starting with a <hl>$</hl>:

<code>
> my $x = {home => "Earth", answer => 42};
{answer => 42, home => Earth}
</code>

It is a hash:

<code>
> $x.^name
Hash
</code>

We can access the values of the individual keys both with quotes around the keys:

<code>
> $x{"home"}
Earth
> $x{"answer"}
42
</code>

And without any quotes:

<code>
> $x&lt;home>
Earth
> $x&lt;answer>
42
</code>

We can fetch the <hl>keys</hl> of the hash and the <hl>values</hl>
separately:

<code>
> $x.keys
(home answer)

> $x.values
(Earth 42)
</code>

What <hl>keys</hl> really returns is a <hl>Seq</hl> (Sequence) object:

<code>
> my $z = $x.keys
(home answer)
> $z.^name
Seq
</code>

We can also iterate over the individual keys using the <hl>for</hl> loop.
($k is the loop variable that will get each key on its turn.)

<code>
> for $x.keys -> $k { say $k }
home
answer
</code>

Using the keys we can access the respective values:

<code>
> for $x.keys -> $k { say $k; say $x{$k} }
home
Earth
answer
42
</code>

Alternatively we can fetch a list of <hl>pairs</hl>
and on each <hl>Pair</hl> object we can call the <hl>key</hl>
and the <hl>value</hl> methods:

<code>
> for $x.pairs -> $p { say $p.^name; say $p.key; say $p.value}
Pair
home
Earth
Pair
answer
42
</code>


<h2>List</h2>

If we wrap the right-hand side in parentheses, we get a <hl>List</hl>
on the left-hand side:

<code>
> $x = ("Earth", "Wind", "Fire")
(Earth Wind Fire)
> $x.^name
List
</code>

In order to iterate over its elements, we need to call the <hl>list</hl>
method on it:

<code>
> for $x.list -> $v { say $v }
Earth
Wind
Fire
</code>

<h2>Array</h2>

If we put square brackets around the values on the right-hand side, we
get an <hl>Array</hl>

<code>
> $x = ["Earth", "Wind", "Fire"]
[Earth Wind Fire]
> $x.^name
Array
</code>

Here too we need to call the <hl>list</hl> method in order to iterate over the elements:

<code>
> for $x.list -> $i { say $i }
Earth
Wind
Fire
</code>

<h2>Forgetting the parentheses</h2>

If you forget the parentheses, Raku will silently forget about all the values except the first one.

<code>
> $x = "Earth", "Wind", "Fire"
(Earth Wind Fire)
> $x.^name
Str
> $x
Earth
</code>


<h2>Conclusion</h2>

The REPL is a nice place to experiment with features, but we have to learn a lot more about Raku to make it useful for us.

